{
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 0.3.5",
   "language": "julia",
   "name": "julia 0.3"
  },
  "language_info": {
   "name": "julia",
   "version": "0.3.5"
  },
  "name": ""
 },
 "nbformat": 3,
 "nbformat_minor": 0,
 "worksheets": [
  {
   "cells": [
    {
     "cell_type": "markdown",
     "metadata": {},
     "source": [
      "**Description**: Shows how to use lazy linear constraints to approximate a nonlinear constraint.\n",
      "\n",
      "**Author**: Iain Dunning\n",
      "\n",
      "**License**: <a rel=\"license\" href=\"http://creativecommons.org/licenses/by-sa/4.0/\"><img alt=\"Creative Commons License\" style=\"border-width:0\" src=\"https://i.creativecommons.org/l/by-sa/4.0/88x31.png\" /></a><br />This work is licensed under a <a rel=\"license\" href=\"http://creativecommons.org/licenses/by-sa/4.0/\">Creative Commons Attribution-ShareAlike 4.0 International License</a>."
     ]
    },
    {
     "cell_type": "markdown",
     "metadata": {},
     "source": [
      "# Approximating a $L^2$ Ball with Linear Constraints\n",
      "\n",
      "Consider the optimization problem\n",
      "\n",
      "$$\n",
      "\\begin{alignat}{2}\n",
      "\\max_{\\mathbf{x}} \\ & \\mathbf{c} \\cdot \\mathbf{x} \\\\\n",
      "\\text{subject to} \\ & \\left\\| x \\right\\| \\leq \\Gamma \\\\\n",
      "                    & x \\in \\mathbb{Z}^n\n",
      "\\end{alignat}\n",
      "$$\n",
      "\n",
      "This problem is an integer second-order cone problem, a problem class that commercial solvers have only really been able to solve efficiently in the past few years. One way to solve this problem would be to solve the continuous relaxation directly, and apply branch-and-bound to find an integer solution. A different perspective is to solve a series of linear problems where the nonlinear norm constraint is approximated by linear constraints.\n",
      "\n",
      "We can implement the second method ourselves using JuMP's **lazy constraint callbacks**. In this method we do not provide the solver with the norm constraint directly, but instead provide a callback that will attempt to generate a new linear constraint whenever the solver finds an integer solution. Only a finite number of linear constraints are needed to approximate the norm constraint, so this algorithm should eventually terminate."
     ]
    },
    {
     "cell_type": "code",
     "collapsed": false,
     "input": [
      "using JuMP\n",
      "# We need to have a solver that supports lazy\n",
      "# constraints. Options include GLPK, Gurobi, and\n",
      "# CPLEX - others may be come available in the future.\n",
      "using GLPKMathProgInterface\n",
      "# using Gurobi\n",
      "\n",
      "# solve_ball\n",
      "# Optimizes the linear function c'x\n",
      "# where ||x|| \u2264 \u0393 + \u03f5, where \u03f5 is an\n",
      "# absolute feasibility tolerance.\n",
      "function solve_ball(c, \u0393, \u03f5=1e-6)\n",
      "    # Size of vector\n",
      "    n = length(c)\n",
      "    \n",
      "    # Create the model\n",
      "    # Make sure to change this line!\n",
      "    m = Model(solver=GLPKSolverMIP())\n",
      "#     m = Model(solver=GurobiSolver(OutputFlag=0))\n",
      "    \n",
      "    # Put the variables in a box\n",
      "    # This ensures the initial solution, before any\n",
      "    # linear constraints are generated, is bounded.\n",
      "    @defVar(m, -\u0393 \u2264 x[1:n] \u2264 +\u0393, Int)\n",
      "    \n",
      "    # Objective is simply the inner product\n",
      "    @setObjective(m, Max, dot(c,x))\n",
      "    \n",
      "    # We'll now build a callback function\n",
      "    # Note that the callback is an inner function,\n",
      "    # or closure. This means that it inherits the\n",
      "    # scope of the enclosing function, including\n",
      "    # the definition of the variables x\n",
      "    # A callback must take a single argument, the\n",
      "    # callback handle. When we want to communicate\n",
      "    # back to the solver, we do so through the\n",
      "    # callback handle\n",
      "    # We'll keep track of how many times the\n",
      "    # callback was called, just for interests sake\n",
      "    num_callbacks = 0\n",
      "    function norm_callback(cb)\n",
      "        num_callbacks += 1\n",
      "        # First, lets get the value of x at the\n",
      "        # current solution. Since we the linear\n",
      "        # constraint we are adding is going to be\n",
      "        # a tangent hyperplane of the L2 ball at\n",
      "        # this point, we can think of the value of\n",
      "        # x as the normal of that hyperplane\n",
      "        N = getValue(x)[:]\n",
      "        \n",
      "        # Lets get the length of the normal\n",
      "        L = norm(N)\n",
      "        \n",
      "        # If the length is small enough...\n",
      "        if L \u2264 \u0393 + \u03f5\n",
      "            # We can stop right now\n",
      "            return\n",
      "        end\n",
      "        \n",
      "        # The constraint is violated, so we need to\n",
      "        # add a tangent hyperplane to the L2 ball\n",
      "        # at this point. We have the normal of the\n",
      "        # plane, and the point we touch the sphere\n",
      "        # is at (\u0393/L)*N, so the plane equation is\n",
      "        # <N,x> = <N,x_0> = <N,(\u0393/L)*N>\n",
      "        #                 = <N,(\u0393/||N||)*N>\n",
      "        #                 = \u0393*||N||\n",
      "        # Note we don't use @addConstraint(m, ...)!\n",
      "        @addLazyConstraint(cb, dot(N,x) \u2264 \u0393*L)\n",
      "    end\n",
      "    \n",
      "    # We now tell JuMP/the solver to use this callback\n",
      "    addLazyCallback(m, norm_callback)\n",
      "    \n",
      "    # Now solve it!\n",
      "    solve(m)\n",
      "    \n",
      "    # Return the solution\n",
      "    return getValue(x)[:], num_callbacks\n",
      "end\n",
      "\n",
      "# Lets generate a random instance\n",
      "srand(1234)\n",
      "# GLPK is very slow with anything bigger\n",
      "# than 2 or 3, Gurobi should be fine up\n",
      "# to at least 10\n",
      "n = 2\n",
      "c = rand(n)\n",
      "\u0393 = 50.0\n",
      "\n",
      "# Solve and display\n",
      "sol, num_callbacks = solve_ball(c, \u0393)\n",
      "println(sol)\n",
      "println(norm(sol))\n",
      "println(num_callbacks)"
     ],
     "language": "python",
     "metadata": {},
     "outputs": [],
     "prompt_number": null
    },
    {
     "cell_type": "code",
     "collapsed": false,
     "input": [
      "# If the problem is small enough, and 2D, we can even\n",
      "# visualize the solution using Compose.jl and set\n",
      "# the vector c with Interact.jl's sliders\n",
      "using Compose, Interact\n",
      "set_default_graphic_size(8cm, 8cm)\n",
      "\n",
      "@manipulate for c1 in -1:0.1:+1, c2 in -1:0.1:+1, log\u03f5 in -4:2\n",
      "    sol, _ = solve_ball([c1,c2], 100, 10.0^log\u03f5)\n",
      "\n",
      "    compose(context(),\n",
      "    # Draw the solution vector\n",
      "    compose(context(),\n",
      "            line([(0.5,0.5),(0.5+sol[1]/300,0.5+sol[2]/300)]),\n",
      "            stroke(\"black\")),\n",
      "    # Draw the intersection point\n",
      "    compose(context(),\n",
      "            circle((0.5 + (100/norm(sol))*sol/300)...,0.02),\n",
      "            fill(\"red\")),\n",
      "    # Draw the feasible region\n",
      "    compose(context(),circle(0.5,0.5,0.333),fill(\"lightblue\"))\n",
      "    )\n",
      "end"
     ],
     "language": "python",
     "metadata": {},
     "outputs": [],
     "prompt_number": null
    },
    {
     "cell_type": "code",
     "collapsed": true,
     "input": [],
     "language": "python",
     "metadata": {},
     "outputs": [],
     "prompt_number": null
    }
   ],
   "metadata": {}
  }
 ]
}
